-- 
-- 	component:   Graphite Reference Model BMM
-- 	description: Graphite Reference Model component formal expression. This file is an ODIN serialisation of
--                   the BMM object meta-model classes found at 
--                   https://www.openEHR.org/releases/LANG/latest/p_bmm.html
-- 	keywords:    reference model, meta-model, archetypes
-- 	author:      Thomas Beale <thomas.beale@graphitehealth.io>
-- 	support:     https://graphite.atlassian.net/issues/
-- 	copyright:   Copyright (c) 2023- Graphite Health <https://www.graphitehealth.io>
-- 	license:     Apache 2.0 <http://www.apache.org/licenses/LICENSE-2.0.html>
--


------------------------------------------------------
-- BMM version on which these schemas are based.
------------------------------------------------------
bmm_version = <"2.3">

------------------------------------------------------
-- schema identification
-- (schema_id computed as <rm_publisher>_<schema_name>_<rm_release>)
------------------------------------------------------
rm_publisher = <"s2">
schema_name = <"entity">
rm_release = <"0.8.6">

model_name = <"ENTITY">

------------------------------------------------------
-- schema documentation
------------------------------------------------------
schema_revision = <"0.8.6.1">
schema_lifecycle_state = <"development">
schema_description = <"S2 Release 0.8.6 Entity model schema"> 
schema_author = <"Thomas Beale <thomas.beale@graphitehealth.io>">

------------------------------------------------------
-- inclusions
------------------------------------------------------
includes = <
	["1"] = <
		id = <"s2_base_0.8.6">
	>
>

------------------------------------------------------
-- packages
------------------------------------------------------
packages = <
	["org.s2.entity"] = <
		name = <"org.s2.entity">
		packages = <
			["entity"] = <
				name = <"entity">
				classes = <"Entity", "Entity_relationship", "Entity_relationship_group">
			>
			["entity_kind"] = <
				name = <"entity_kind">
				classes = <"Entity_kind", "Entity_kind_relationship", "Entity_kind_relationship_group">
				packages = <
					["continuant"] = <
						name = <"continuant">
						classes = <"Physical_entity_kind", "Materially_dependent_entity_kind", "Material_entity_kind", "Object_extension_kind", 
							"Object_aggregate_kind", "Independent_object_kind", "Space", "Artifact_kind", "Substance_kind", 
							"Biological_entity_kind", "Facility_kind", "Material_location", "Spatial_region", "Fiat_point", "Fiat_line", 
							"Fiat_surface", "Geographical_site">
					>
					["occurrent"] = <
						name = <"occurrent">
						classes = <"Occurrent_kind", "Process_kind">
					>
				>
			>
			["physical_entity"] = <
				name = <"physical_entity">
				classes = <"Physical_entity", "Material_entity">
			>
			["social_entity"] = <
				name = <"social_entity">
				classes = <"Social_entity", "Party", "Persona", "Person", "Organization", "Org_unit", "Org_entity", 
					"Agent", "Aggregate_agent", "Individual_agent", "Automaton", "Team", "Party_relationship", 
					"Accountability">
			>
			["occurrent"] = <
				name = <"occurrent">
				classes = <"Process", "Care_process">
			>
			["resource"] = <
				name = <"resource">
				classes = <"Resource_use", "Service_use", "Consumable_use">
			>
		>
	>
>

------------------------------------------------------
-- classes
------------------------------------------------------

class_definitions = <

	------------------------------------------------------
	-- entity
	------------------------------------------------------

	["Entity"] = <
		name = <"Entity">
		ancestors = <"Locatable">
		is_abstract = <True>
		properties = <
			["domain_type"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"domain_type">
				type = <"Terminology_term">
				is_mandatory = <True>
			>
			["relationships"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"relationships">
				type_def = <
					container_type = <"List">
					type = <"Entity_relationship_group">
				>
			>
			["other_details"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"other_details">
				type_def = <
					container_type = <"List">
					type = <"Node">
				>
			>
		>
	>

	["Entity_relationship"] = <
		name = <"Entity_relationship">
		ancestors = <"Locatable">
		is_abstract = <True>
		properties = <
			["domain_type"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"domain_type">
				type = <"Terminology_term">
				is_mandatory = <True>
			>
			["source"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"source">
				type = <"Object_ref">
				is_mandatory = <True>
			>
			["target"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"target">
				type = <"Object_ref">
				is_mandatory = <True>
			>
			["other_details"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"other_details">
				type_def = <
					container_type = <"List">
					type = <"Node">
				>
			>
			["time_validity"] = (P_BMM_GENERIC_PROPERTY) <
				name = <"time_validity">
				type_def = <
					root_type = <"Interval">
					generic_parameters = <"Date">
				>
				is_im_runtime = <True>
			>
		>
	>

	["Entity_relationship_group"] = <
		name = <"Entity_relationship_group">
		ancestors = <"Locatable">
		is_abstract = <True>
		properties = <
			["domain_type"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"domain_type">
				type = <"Terminology_term">
				is_mandatory = <True>
			>
			["members"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"members">
				type_def = <
					container_type = <"List">
					type = <"Object_ref">
				>
			>
		>
	>

	------------------------------------------------------
	-- social_entity
	------------------------------------------------------

	["Social_entity"] = <
		name = <"Social_entity">
		ancestors = <"Entity">
		is_abstract = <True>
	>

	["Party"] = <
		name = <"Party">
		is_abstract = <True>
		ancestors = <"Social_entity">
		properties = <
			["identifiers"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"identifiers">
				type_def = <
					container_type = <"List">
					type = <"Id_use">
				>
			>
			["identities"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"identities">
				type_def = <
					container_type = <"List">
					type = <"Party_identity">
				>
				is_mandatory = <True>
				cardinality = <|>=1|>
			>
			["location_addresses"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"location_addresses">
				type_def = <
					container_type = <"List">
					type = <"Location_address_use">
				>
			>
			["comms_addresses"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"comms_addresses">
				type_def = <
					container_type = <"List">
					type = <"Comms_address_use">
				>
			>
			["accountability_types"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"accountability_types">
				type_def = <
					container_type = <"List">
					type = <"Accountability">
				>
			>
		>
	>
    
	["Agent"] = <
		name = <"Agent">
		is_abstract = <True>
		ancestors = <"Party">
		properties = <
			["personas"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"personas">
				type_def = <
					container_type = <"List">
					type = <"Persona">
				>
				cardinality = <|>=1|>
			>
			["languages"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"languages">
				type_def = <
					container_type = <"List">
					type = <"Language_capability">
				>
			>
		>
	>

	["Aggregate_agent"] = <
		name = <"Aggregate_agent">
		ancestors = <"Agent">
		is_abstract = <True>
	>

	["Individual_agent"] = <
		name = <"Individual_agent">
		ancestors = <"Agent">
		is_abstract = <True>
	>

	["Org_entity"] = <
		name = <"Org_entity">
		ancestors = <"Aggregate_agent">
		is_abstract = <True>
	>

	["Org_unit"] = <
		name = <"Org_unit">
		ancestors = <"Org_entity">
	>

	["Organization"] = <
		name = <"Organization">
		ancestors = <"Org_entity">
	>

	["Team"] = <
		name = <"Team">
		ancestors = <"Aggregate_agent">
	>

	["Person"] = <
		name = <"Person">
		ancestors = <"Individual_agent">
	>

	["Automaton"] = <
		name = <"Automaton">
		ancestors = <"Individual_agent">
	>

	["Persona"] = <
		name = <"Persona">
		ancestors = <"Party">
		properties = <
			["performer"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"performer">
				type = <"Object_ref">
				is_mandatory = <True>
				is_im_runtime = <True>
			>
			["time_validity"] = (P_BMM_GENERIC_PROPERTY) <
				name = <"time_validity">
				type_def = <
					root_type = <"Interval">
					generic_parameters = <"Date">
				>
				is_im_runtime = <True>
			>
			["capabilities"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"capabilities">
				type_def = <
					container_type = <"List">
					type = <"Capability">
				>
				cardinality = <|>=1|>
			>
		>
	>

	["Party_relationship"] = <
		name = <"Party_relationship">
		ancestors = <"Entity_relationship">
		properties = <
			["scoper"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"scoper">
				type = <"String">
			>
		>
	>

	["Accountability"] = <
		name = <"Accountability">
		ancestors = <"Locatable">
		properties = <
			["description"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"description">
				type_def = <
					container_type = <"List">
					type = <"Node">
				>
				cardinality = <|>=1|>
			>
		>
	>

	["Id_use"] = <
		name = <"Id_use">
		ancestors = <"Info_node">
		properties = <
			["original_id"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"original_id">
				type = <"Rwe_id_ref">
			>
		>
	>

	------------------------------------------------------
	-- Entity kind
	------------------------------------------------------

	["Entity_kind"] = <
		name = <"Entity_kind">
		ancestors = <"Locatable">
		is_abstract = <True>
		properties = <
			["items"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"items">
				type_def = <
					container_type = <"List">
					type = <"Node">
				>
			>
		>
	>

	["Entity_kind_relationship"] = <
		name = <"Entity_kind_relationship">
		ancestors = <"Locatable">
		properties = <
			["domain_type"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"domain_type">
				type = <"Terminology_term">
				is_mandatory = <True>
			>
			["source"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"source">
				type = <"Object_ref">
				is_mandatory = <True>
			>
			["target"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"target">
				type = <"Object_ref">
				is_mandatory = <True>
			>
		>
	>

	["Entity_kind_relationship_group"] = <
		name = <"Entity_kind_relationship_group">
		ancestors = <"Locatable">
		properties = <
			["type"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"type">
				type = <"Terminology_term">
				is_mandatory = <True>
			>
			["members"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"members">
				type_def = <
					container_type = <"List">
					type = <"Object_ref">
				>
			>
		>
	>

	["Physical_entity_kind"] = <
		name = <"Physical_entity_kind">
		ancestors = <"Entity_kind">
		is_abstract = <True>
	>

	["Materially_dependent_entity_kind"] = <
		name = <"Materially_dependent_entity_kind">
		ancestors = <"Physical_entity_kind">
		is_abstract = <True>
	>

	["Spatial_region"] = <
		name = <"Spatial_region">
		ancestors = <"Physical_entity_kind">
		is_abstract = <True>
	>

	["Material_entity_kind"] = <
		name = <"Material_entity_kind">
		ancestors = <"Materially_dependent_entity_kind">
		is_abstract = <True>
	>

	["Object_aggregate_kind"] = <
		name = <"Object_aggregate_kind">
		ancestors = <"Material_entity_kind">
	>

	["Facility_kind"] = <
		name = <"Facility_kind">
		ancestors = <"Object_aggregate_kind">
	>

	["Object_extension_kind"] = <
		name = <"Object_extension_kind">
		ancestors = <"Material_entity_kind">
		is_abstract = <True>
		properties = <
			["parts"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"parts">
				type_def = <
					container_type = <"List">
					type = <"Object_extension_kind">
				>
			>
		>
	>

	["Independent_object_kind"] = <
		name = <"Independent_object_kind">
		ancestors = <"Object_extension_kind">
	>

	["Space"] = <
		name = <"Space">
		ancestors = <"Object_extension_kind">
		properties = <
			["parts"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"parts">
				type_def = <
					container_type = <"List">
					type = <"Space">
				>
			>
		>
	>

	["Artifact_kind"] = <
		name = <"Artifact_kind">
		ancestors = <"Independent_object_kind">
	>

	["Substance_kind"] = <
		name = <"Substance_kind">
		ancestors = <"Independent_object_kind">
	>

	["Biological_entity_kind"] = <
		name = <"Biological_entity_kind">
		ancestors = <"Independent_object_kind">
	>

	["Material_location"] = <
		name = <"Material_location">
		ancestors = <"Materially_dependent_entity_kind">
		is_abstract = <True>
	>

	["Fiat_point"] = <
		name = <"Fiat_point">
		ancestors = <"Material_location">
	>

	["Fiat_line"] = <
		name = <"Fiat_line">
		ancestors = <"Material_location">
		properties = <
			["is_closed"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"is_closed">
				type = <"Boolean">
			>
		>
	>

	["Fiat_surface"] = <
		name = <"Fiat_surface">
		ancestors = <"Material_location">
		properties = <
			["is_closed"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"is_closed">
				type = <"Boolean">
			>
		>
	>

	["Geographical_site"] = <
		name = <"Geographical_site">
		ancestors = <"Fiat_line">
	>

	------------------------------------------------------
	-- Physical entity
	------------------------------------------------------

	["Physical_entity"] = <
		name = <"Physical_entity">
		ancestors = <"Entity">
		generic_parameter_defs = <
			["K"] = <
				name = <"K">
				conforms_to_type = <"Physical_entity_kind">
			>
		>
		properties = <
			["kind"] = (P_BMM_SINGLE_PROPERTY_OPEN) <
				name = <"kind">
				type = <"K">
			>
			["kind_ref"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"kind_ref">
				type = <"Object_ref">
			>
		>
	>

	["Material_entity"] = <
		name = <"Material_entity">
		ancestors = <"Physical_entity">
		generic_parameter_defs = <
			["K"] = <
				name = <"K">
				conforms_to_type = <"Material_entity_kind">
			>
		>
		properties = <
			["identifier"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"identifier">
				type = <"Object_ref">
			>
			["other_identifiers"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"identifiers">
				type_def = <
					container_type = <"List">
					type = <"Id_use">
				>
			>
			["individual_attributes"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"individual_attributes">
				type_def = <
					container_type = <"List">
					type = <"Node">
				>
			>
			["lot_information"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"lot_information">
				type_def = <
					container_type = <"List">
					type = <"Node">
				>
			>
			["use_details"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"use_details">
				type_def = <
					container_type = <"List">
					type = <"Node">
				>
			>
		>
	>

	------------------------------------------------------
	-- Entity_kind.occurrent
	------------------------------------------------------

	["Occurrent_kind"] = <
		name = <"Occurrent_kind">
		ancestors = <"Entity_kind">
		is_abstract = <True>
	>

	["Process_kind"] = <
		name = <"Process_kind">
		ancestors = <"Entity_kind">
	>

	------------------------------------------------------
	-- Entity.occurrent
	------------------------------------------------------

	["Process"] = <
		name = <"Process">
		ancestors = <"Entity">
		properties = <
			["start_time"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"start_time">
				type = <"Date_time">
			>
			["end_time"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"end_time">
				type = <"Date_time">
			>
			["participations"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"participations">
				type_def = <
					container_type = <"List">
					type = <"Participation">
				>
				cardinality = <|>=1|>
			>
		>
	>

	["Care_process"] = <
		name = <"Care_process">
		ancestors = <"Process">
		properties = <
			["subject"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"subject">
				type = <"Entity_ref_node">
			>
			["responsible_organization"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"responsible_organization">
				type = <"Entity_ref_node">
			>
		>
	>

	------------------------------------------------------
	-- resource
	------------------------------------------------------

	["Resource_use"] = <
		name = <"Resource_use">
		ancestors = <"Locatable">
		is_abstract = <True>
		properties = <
			["start_time"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"start_time">
				type = <"Date_time">
				is_mandatory = <True>
			>
			["duration"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"duration">
				type = <"Duration">
			>
			["cost_data"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"cost_data">
				type_def = <
					container_type = <"List">
					type = <"Node">
				>
			>
			["description"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"description">
				type_def = <
					container_type = <"List">
					type = <"Node">
				>
			>
		>
	>

	["Service_use"] = <
		name = <"Service_use">
		ancestors = <"Resource_use">
	>

	["Consumable_use"] = <
		name = <"Consumable_use">
		ancestors = <"Resource_use">
		properties = <
			["amount"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"amount">
				type = <"Decimal">
			>
		>
	>

>

