template (adl_version=2.0.6; rm_release=0.8.0)
	s2-EHR-Composition.t_lab_report-generic.v1.0.0

specialize
	s2-EHR-Composition.lab_report.v1

language
	original_language = <[iso_639-1::en]>

description
	lifecycle_state = <"unmanaged">
	original_author = <
		["name"] = <"Thomas Beale <thomas.beale@GraphiteHealth.io>">
		["organisation"] = <"Graphite Health <http://www.GraphiteHealth.io>">
	>
	copyright = <"Copyright (c) 2023 Graphite Health">
	licence = <"Creative Commons CC-BY 4.0 unported <http://creativecommons.org/>">
	details = <
		["en"] = <
			language = <[iso_639-1::en]>
			purpose = <"Generic lab report">
		>
	>

definition
	Composition[id1.1] matches {
		content matches {
			Lab_result[id0.1] matches {	-- Lab panel result
				data matches {
					use_archetype Node[id0.90, s2-EHR-Node.lab_analyte.v0] 
				}
				qualifiers matches {
					Node[id0.10] matches {	-- Device
						value matches {
							Coded_text[id0.101] 
						}
					}
					Node[id0.11] matches {	-- Method
						value matches {
							Coded_text[id0.102] 
						}
					}
					Node[id0.1060] occurrences matches {0..1} matches {	-- Clinical interpretation
						value matches {
							Text[id0.1065] 
						}
					}
					use_archetype Node[id0.20, s2-EHR-Node.t_lab_report-generic-org.v1] 
				}
			}
		}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1.1"] = <
				text = <"Generic lab report">
				description = <"Lab report structure consisting of any number of panel results">
			>
			["id0.1"] = <
				text = <"Lab panel">
				description = <"Lab panel result">
			>
			["id0.90"] = <
				text = <"Analyte">
				description = <"-">
			>
			["id0.10"] = <
				text = <"Device">
				description = <"-">
			>
			["id0.11"] = <
				text = <"Method">
				description = <"-">
			>
			["id0.20"] = <
				text = <"Performing laboratory">
				description = <"-">
			>
			["id0.1060"] = <
				text = <"Clinical interpretation">
				description = <"Single word, phrase or brief description that represents the clinical meaning and significance of the blood pressure measurement.">
			>
		>
	>

-------------------------------------------------------------
template_overlay
	s2-EHR-Node.t_lab_report-generic-org.v1.0.0

specialize
	s2-EHR-Node.organization.v1

definition
	Node[id1.1] matches {
		/items matches {
			Node[id5] occurrences matches {0}
			use_archetype Node[id6, s2-EHR-Node.t_lab_report-generic-address.v1] 
			use_archetype Node[id23, s2-EHR-Node.t_lab_report-generic-elec_comm.v1] 
			use_archetype Node[id3, s2-EHR-Node.t_lab_report-generic-person.v1] 
			use_archetype Node[id22, s2-EHR-Node.organization.v1] occurrences matches {0}
			allow_archetype Node[id18]occurrences matches {0}
			Node[id20] occurrences matches {0} 
		}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1.1"] = <
				text = <"Simplified organization">
				description = <"Simplified organization">
			>
		>
	>

-------------------------------------------------------------
template_overlay
	s2-EHR-Node.t_lab_report-generic-address.v1.0.0

specialize
	s2-EHR-Node.address.v1

definition
	Node[id1.1] matches {
		value matches {
			Plain_text[id0.1] 
		}
		/items existence matches {0} 
	}

terminology
	term_definitions = <
		["en"] = <
			["id1.1"] = <
				text = <"Simplified address">
				description = <"Simplified address">
			>
		>
	>

-------------------------------------------------------------
template_overlay
	s2-EHR-Node.t_lab_report-generic-elec_comm.v1.0.0

specialize
	s2-EHR-Node.electronic_communication.v1

definition
	Node[id1.1] matches {	
		value matches {
			Plain_text[id0.1] 
		}
		/items existence matches {0} 
	}

terminology
	term_definitions = <
		["en"] = <
			["id1.1"] = <
				text = <"Simplified electronic communication">
				description = <"Simplified electronic communication">
			>
		>
	>

-------------------------------------------------------------
template_overlay
	s2-EHR-Node.t_lab_report-generic-person.v1.0.0

specialize
	s2-EHR-Node.person.v1

definition
	Node[id1.1] matches {	
		value matches {
			Plain_text[id0.1] 
		}
		/items existence matches {0} 
	}

terminology
	term_definitions = <
		["en"] = <
			["id1.1"] = <
				text = <"Simplified contact person">
				description = <"Simplified contact person">
			>
		>
	>


