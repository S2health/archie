archetype (adl_version=2.1.0; rm_release=0.8.5; uid=bf2c9acf-e431-4cef-8958-0af57dc9ab84 )
	s2-EHR-Composition.lab_report.v1.0.0

language
	original_language = <[iso_639-1::en]>

description
	lifecycle_state = <"unmanaged">
	original_author = <
		["name"] = <"Thomas Beale <thomas.beale@GraphiteHealth.io>">
		["organisation"] = <"Graphite Health <http://www.GraphiteHealth.io>">
	>
	custodian_namespace = <"org.s2health">
	custodian_organisation = <"S2 Health <http://s2health.org>">
	copyright = <"Copyright (c) 2023 S2 Health">
	licence = <"Creative Commons CC-BY 4.0 unported <http://creativecommons.org/>">
	details = <
		["en"] = <
			language = <[iso_639-1::en]>
			purpose = <"Generic lab report">
		>
	>

definition
	Composition[id1] matches {	-- Lab report
		code matches {
			Terminology_term[id10] matches {
				concept matches {[at1]}
			}
		}
		category matches {
			Terminology_term[id11] matches {
				concept matches {[at2]}
				description matches {"persistent"}
			}
		}
		context matches {
			Event_context[id2] occurrences matches {1} matches {
				health_care_facility existence matches {1}  matches {
					use_archetype Entity_ref_node[id18, s2-EHR-Entity_ref_node.lab_organization_ref.v1] 
				}
				other_context matches {
					Info_node[id3] occurrences matches {1} matches {	-- Report identifier
						value matches {
							Rwe_id_ref[id8] 
						}
					}
					Info_node[id9] occurrences matches {1} matches {	-- Report status
						value matches {
							Coded_text[id10] matches {
								term matches {
									Terminology_term[id110] matches {
										concept matches {[ac1]}		-- Lab report status
									}
								}
							}
						}
					}
				}
				encounter matches {
					use_archetype Entity_ref_node[id20, s2-EHR-Entity_ref_node.encounter_ref.v1] 
				}
			}
		}
		author matches {
			use_archetype Entity_ref_node[id21, s2-EHR-Entity_ref_node.healthcare_professional_ref.v1] 
		}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1"] = <
				text = <"Lab report">
				description = <"-">
			>
			["id3"] = <
				text = <"Report identifier">
				description = <"-">
			>
			["id9"] = <
				text = <"Report status">
				description = <"-">
			>
			["id4"] = <
				text = <"Result">
				description = <"-">
			>
			["id5"] = <
				text = <"Result">
				description = <"-">
			>
			["id18"] = <
				text = <"Laboratory organization">
				description = <"Laboratory organization">
			>
			["id20"] = <
				text = <"Billing encounter">
				description = <"Associated billing encounter">
			>
			["id21"] = <
				text = <"Author">
				description = <"Authoring healthcare professional">
			>
			["ac1"] = <
				text = <"Lab report status">
				description = <"-">
			>
			["at1"] = <
				text = <"Lab report">
				description = <"Is-about code for this Composition: lab report">
			>
			["at2"] = <
				text = <"Persistent Composition category">
				description = <"Persistent Composition category">
			>
		>
	>
	term_bindings = <
		["loinc"] = <
			["id9"] = <http://loinc.org/91582-7>
			["id3"] = <http://loinc.org/90084-5>
		>
		["openehr"] = <
			["at2"] = <http://openehr.org/id/431>
		>
		["s2"] = <
			["ac1"] = <http://s2health.org/id/s2.LabResultStatus>
		>
	>

